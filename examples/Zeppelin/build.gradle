import groovyx.net.http.RESTClient
import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // we need rest + json to configure zeppelin interpreters
        classpath 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    }
}

plugins {
  // ssh plugin documentation: https://gradle-ssh-plugin.github.io/docs/
  id 'org.hidetake.ssh' version '1.5.0'
}

// load some common helper methods
apply from: "${projectDir}/../../shared/common-helpers.gradle"

// get the cluster connection details
Properties props = new Properties()
props.load(new FileInputStream("$projectDir/../../connection.properties"))

def ZEPPELIN='zeppelin-0.6.0-bin-all'


task Uninstall << {
    ssh.run {
        // remotes.bicluster is defined in shared/common-helpers.gradle
        session(remotes.bicluster) {
            try {
                // initialise kerberos
                execute "kinit -k -t ${props.username}.keytab ${props.username}@IBM.COM"
            } 
            catch (Exception e) {
                println "problem running kinit - maybe this is a Basic cluster?"
            }
            put from: "${projectDir}/zeppelin_uninstall.sh", 
                into: "./zeppelin_uninstall.sh"

            put from: "${projectDir}/Pyspark_Test.json", 
                into: "./Pyspark_Test.json"

            execute "bash ./zeppelin_uninstall.sh"
        }
    }
}

def configInterpreters = { port ->

    def url = "http://localhost:${port}/"

    def client = new RESTClient( url )
    def resp = client.get( path : 'api/interpreter/setting' )

    assert resp.status == 200

    resp.data.body.each { interpreter ->
        println interpreter.id
    }
}

task Install << {
    def zeppelin_env = file("${projectDir}/zeppelin_env.sh")

    def matcher = ( zeppelin_env.text =~ /export\s*ZEPPELIN_PORT=([0-9]+)\s*/ )
    def zeppelin_port = matcher[0][1]

    ssh.run {
        // remotes.bicluster is defined in shared/common-helpers.gradle
        session(remotes.bicluster) {
            try {
                // initialise kerberos
                execute "kinit -k -t ${props.username}.keytab ${props.username}@IBM.COM"
            } 
            catch (Exception e) {
                println "problem running kinit - maybe this is a Basic cluster?"
            }
            put from: "${projectDir}/zeppelin_env.sh", 
                into: "./zeppelin_env.sh"

            put from: "${projectDir}/zeppelin_install.sh", 
                into: "./zeppelin_install.sh"

            execute "bash ./zeppelin_install.sh"

            def local_port = forwardLocalPort hostPort: zeppelin_port.toInteger()

            // FIXME we seem to need to wait for port forwarding to be setup, but
            //       we should have a better mechanism than sleep for ensuring port
            //       forwarding is available
            sleep(5000)

            configInterpreters(local_port)

            // restart zeppelin
            execute "./${ZEPPELIN}/bin/zeppelin-daemon.sh restart"
        }
    }
}


task Run << {

    def zeppelin_env = file("${projectDir}/zeppelin_env.sh")

    def matcher = ( zeppelin_env.text =~ /export\s*ZEPPELIN_PORT=([0-9]+)\s*/ )
    def zeppelin_port = matcher[0][1]

    ssh.run {
        session(remotes.bicluster) {
            
            // make sure zeppelin is installed
            try {
                execute "[[ -d ./${ZEPPELIN} ]]"
            } 
            catch (Exception e) {
                throw new GradleException(
                    "Couldn't find the Zeppelin installation.  Try running './gradlew Install'."
                    )
            }

            // make sure zeppelin is running
            execute "./${ZEPPELIN}/bin/zeppelin-daemon.sh status || ./${ZEPPELIN}/bin/zeppelin-daemon.sh start"
 
            def port = forwardLocalPort hostPort: zeppelin_port.toInteger()
            def console = System.console()
            if (console) {
                def output = "\n\n" +
                    "SSH forwarding localhost:${port} -> BigInsights zeppelin localhost:${zeppelin_port}\n\n" +
                    "You can now access zeppelin from your local machine using the URL: http://localhost:${port}.\n" +
                    "\n" +
                    "Press ENTER or CTRL+C to quit.\n" +
                    "\n" +
                    "NOTE: If you quit you will lose your connection to Zeppelin.\n" +
                    "      After quitting you can execute 'Run' again to connect to Zeppelin.\n" +
                    "      Every time you execute 'Run' you will get a different port to\n" +
                    "      connect to on your local machine.\n\n"
                console.readLine(output)
            } else {
                logger.error "Cannot get console."
            }
        }
    }
}

